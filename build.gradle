plugins {
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.7"
}

subprojects {
    apply plugin: 'java'

    java.toolchain.languageVersion = JavaLanguageVersion.of("${java_version}")
    // Uncomment to generate sources jar file
    //java.withSourcesJar()
    // Uncomment to generate javadoc file
    //java.withJavadocJar()

    jar {
        manifest {
            attributes([
                    'Specification-Title'       : "${mod_name}",
                    'Specification-Vendor'      : "${mod_authors}",
                    'Specification-Version'     : "${project.jar.archiveVersion}",
                    'Implementation-Title'      : "${project.name}",
                    'Implementation-Version'    : "${project.jar.archiveVersion}",
                    'Implementation-Vendor'     : "${mod_authors}",
                    'Implementation-Timestamp'  : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Timestamp'                 : System.currentTimeMillis()
            ])
        }
    }

    repositories {
        mavenCentral()

        maven {
            name = "Sponge / Mixin"
            url  = "https://repo.spongepowered.org/repository/maven-public/"
        }
        maven {
            name = "Configuration"
            url = "https://repsy.io/mvn/toma/public/"
        }
        flatDir {
            dir './libs'
        }
    }

    processResources {
        def props = [
                // Mod
                "version": project.version,
                "mod_id": "${mod_id}",
                "mod_name": "${mod_name}",
                "mod_authors": "${mod_authors}",
                "mod_licence": "${mod_licence}",
                "mod_description": "${mod_description}",
                "mod_group": "${mod_group}",

                // Minecraft env
                "java_version": "${java_version}",
                "minecraft_version": "${minecraft_version}",
                "minecraft_version_range": "${minecraft_version_range}",
                "vanilla_gradle_version": "${vanilla_gradle_version}",

                // Mixin env
                "mixin_loader": "${mixin_loader}",
                "mixin_version": "${mixin_version}",
                "mixin_min_version": "${mixin_min_version}",

                // Forge
                "forge_version": "${forge_version}",
                "forge_version_range": "${forge_version_range}",
                "forge_gradle_version": "${forge_gradle_version}",

                // Fabric
                "fabric_version": "${fabric_version}",
                "fabric_loader_version": "${fabric_loader_version}",
                "fabric_loom": "${fabric_loom}",
                "fabric_minecraft_version_range": "${fabric_minecraft_version_range}",

                // Dependencies
                "configuration_version_range": "${configuration_version_range}"
        ]

        filesMatching(["pack.mcmeta", "fabric.mod.json", "META-INF/*.toml", "*.mixins.json"]) {
            expand(props)
        }
        inputs.properties(props)
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.getRelease().set(project.java_version.toInteger())
    }
}